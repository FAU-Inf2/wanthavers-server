plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

//application entry point
def mainClass = 'de.fau.cs.mad.wanthavers.server.ServerApplication'
//set the dropwizard version to use here
def dropwizardVersion = '0.9.2'
version = '0.1'

repositories {
    mavenCentral()

    maven {
        credentials {
            username 'anonymous'
            password 'anonymous'
        }
        url "http://mad.cs.fau.de:8081/content/repositories/releases/"
    }
}

build.dependsOn(":wanthavers-common:build")

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile project(':wanthavers-common')

    compile 'org.eclipse.persistence:javax.persistence:2.1.0'
    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-client:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-assets:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-auth:' + dropwizardVersion
    compile 'org.glassfish.jersey.ext:jersey-proxy-client:2.17'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.3'
    compile 'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion
    compile 'com.h2database:h2:1.4.186'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.5.3'
    compile 'org.json:json:20160212'
    compile 'com.github.philsippl:parse4j:1.5'
    compile 'com.amazonaws:aws-java-sdk:1.11.3'
    compile 'net.coobird:thumbnailator:0.4.8'
}

flyway {
    String jdbcURL = System.getenv("DW_DATABASE_URL");
    if(jdbcURL == null) {
        jdbcURL = "jdbc:h2:./wanthavers-db";
    }
    url = jdbcURL
    user = 'sa'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

mainClassName = mainClass
run {
    args 'server', 'config.yml'
}
